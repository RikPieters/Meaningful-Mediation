TITLE:
  Study 3 in: Kupor, Daniella M and Zakary L. Tormala (2015), 
  "Persuasion, Interrupted: The Effect of Momentary Interruptions 
  on Message Processing and Persuasion," Journal of Consumer Research,
  vol 42, 300-315.
  ! Comments are behind exclamation mark (!)
  ! SSD data analysis

  ! Data are partially standardized (= ps)
  ! Gives the same results as in the appendix of the meaningful mediation paper
DATA:
  FILE = Coffee_ssd_ps.dat;              ! Data are partially standardized
  TYPE = means stdeviations correlation; ! Information in SSD file 
  NOBSERVATIONS = 223;                   ! Sample size
VARIABLE:
  NAMES = conditio curiosit tho_fav beh_int;
MODEL:
  curiosit ON conditio (a1);        ! curiosity <- condition (a1-path) 
  tho_fav  ON conditio (a2);        ! and so on. Labels are between (), and ..
  tho_fav  ON curiosit (d);         ! .. are referred to later in NEW
  beh_int  ON conditio (cp); 
  beh_int  ON curiosit (b1);
  beh_int  ON tho_fav  (b2);
MODEL CONSTRAINT:
  ! make NEW variables from the earlier labels between ()
  NEW (a1b1 a2b2 a1db2 totmed total pm_a1db2 pm_tot dif_d);
  a1b1 = a1*b1;                     ! Effect of conditio on beh_int via curiosity
  a2b2 = a2*b2;                     ! Effect of conditio on beh_int via tho_fav
  a1db2 = a1*d*b2;                  ! Sequential mediation
  totmed = a1b1 + a2b2 + a1db2;     ! Total mediation: sum of 3 mediated paths
  total = totmed + cp;              ! Total effect
  pm_a1db2 = a1db2 / total;         ! Percentage mediated serial
  pm_tot = totmed / total;          ! Percentage mediated total
  dif_d  = totmed - cp;             ! Difference test: total indirect - conditional direct
OUTPUT: 
  STDY;                             ! R-square