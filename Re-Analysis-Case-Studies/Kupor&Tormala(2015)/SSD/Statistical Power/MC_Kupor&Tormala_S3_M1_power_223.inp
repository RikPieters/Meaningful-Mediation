TITLE:
 MONTE SIMULATION TO ESTIMATE STATISTICAL POWER
  Study 3 in: Kupor, Daniella M and Zakary L. Tormala (2015), 
  "Persuasion, Interrupted: The Effect of Momentary Interruptions 
  on Message Processing and Persuasion," Journal of Consumer Research,
  vol 42, 300-315.

  ! assesses the statistical power of the results, 
  ! using Monte Carlo analysis, with estimated parameters
  ! following Muthen, Muthen, and Asparouhov (2016) and references there
MONTECARLO:
  NAMES = conditio beh_int tho_fav curiosit;
  NOBSERVATIONS = 223; ! sample size
  NREPS = 1000;        ! 1000 replication samples generated
MODEL POPULATION:
  [conditio@0.501]; conditio@.251; ! observed mean and variance of X
  curiosit ON conditio*0.321 (a1); ! parameter estimates of partially stand results
  tho_fav  ON conditio*0.257 (a2);
  tho_fav  ON curiosit*0.186 (d);
  beh_int  ON conditio*0.233 (cp);
  beh_int  ON curiosit*0.267 (b1);
  beh_int  ON tho_fav*0.342 (b2);
  [beh_int*-.119];  ! intercept
  [tho_fav*-.131];
  [curiosit*-.164];
  beh_int*0.739;  ! residual variance
  tho_fav*0.941;
  curiosit*0.974;
MODEL:
  [conditio@0.501]; conditio@.251; ! observed mean and variance of X
  curiosit ON conditio*0.321 (a1);
  tho_fav  ON conditio*0.257 (a2);
  tho_fav  ON curiosit*0.186 (d);
  beh_int  ON conditio*0.233 (cp);
  beh_int  ON curiosit*0.267 (b1);
  beh_int  ON tho_fav*0.342 (b2);
  [beh_int*-.119];  ! intercept
  [tho_fav*-.131];
  [curiosit*-.164];
  beh_int*0.739;  ! residual variance
  tho_fav*0.941;
  curiosit*0.974;
MODEL CONSTRAINT:
  NEW(a1b1*0.086 a2b2*0.088 a1db2*0.020 totmed*0.194 total*0.426);
  a1b1 = a1*b1;
  a2b2 = a2*b2;
  a1db2 = a1*d*b2;
  totmed = a1b1 + a2b2 + a1db2;
  total = totmed + cp;
