TITLE:
  Re-analysis of Experiment 2 in:
  Kim Sara and Ann L. McGill (2011), 
  "Gaming with Mr. Slot or Gaming with the Slot Machine?
  Power, Antropomorphism, and Risk Perception,"
  Journal of Consumer Research, 38 (1), 94-107

  ! SSD: Summary Statistics Data
  ! Inspect STDY as M and Y are standardized in paper

  Model 1 is path mediation model
DATA:
  FILE = Experiment2-SSD_us.dat; ! SSD as in Appendix. Results as in Kim and McGill (2011)
  TYPE = means stdeviations correlation; 
  NOBSERVATIONS = 84; 
VARIABLE:
  NAMES = x1 x2 x1x2 m y;
ANALYSIS:
MODEL:
  m ON x1 (a1);    ! regress M ON X1 and label the estimate a1  
  m ON x2 (a2);    ! regress M ON X2 and label the estimate a2 
  m ON x1x2 (a3);  ! regress M ON X1X2 and label the estimate a3
  y ON m (b);      ! regress Y ON M and label the estimate b
  y ON x1 (cp1);   ! regress Y ON X1X2 and label the estimate cp1 (= c-prime1)
  y ON x2 (cp2);   ! regress Y ON X1X2 and label the estimate cp2 (= c-prime2)
  y ON x1x2 (cp3); ! regress Y ON X1X2 and label the estimate cp3 (= c-prime3)
MODEL CONSTRAINT:  
! make the required new variables        
  new (a1b a2b a3b cprime1 cprime2 cprime3 total totX1X2 diff pmX1X2);   
  a1b = a1*b;                ! mediated effect of X1
  a2b = a2*b;                ! mediated effect of X2
  a3b = a3*b;                ! mediated effect of X1X2
  cprime1 = cp1;             ! direct effect of X1
  cprime2 = cp2;             ! direct effect of X2
  cprime3 = cp3;             ! direct effect of X1X2 
  total = a1b+a2b+a3b+cp1+cp2+cp3; ! total effect overall of Xs on Y
  totX1X2 = a3*b + cp3;      ! total effect of X1X2 on Y
  diff = a3b - cp3;          ! difference indirect and conditional direct effect
  pmX1X2 = (a3b/(a3b+cp3))*100; ! percentage final effect of X1X2 which is mediated
OUTPUT:   
  STDY;                      ! R-SQUARE
  SAMPSTAT;                  ! Sample Statistics (M, SD, corr)
