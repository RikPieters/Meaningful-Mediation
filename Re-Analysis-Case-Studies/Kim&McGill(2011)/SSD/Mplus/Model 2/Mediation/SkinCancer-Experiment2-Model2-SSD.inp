TITLE:
  Re-analysis of Experiment 2 in:
  Kim Sara and Ann L. McGill (2011), 
  "Gaming with Mr. Slot or Gaming with the Slot Machine?
  Power, Antropomorphism, and Risk Perception,"
  Journal of Consumer Research, 38 (1), 94-107

  ! SEM Mediation Model on SSD (item-level)
  ! Results of SSD analyses converge to but are not identical to raw data analyses
DATA:
  FILE = SkinCancer-Experiment2-item-SSD.dat;
  TYPE = means stdeviations correlation; 
  NOBSERVATIONS = 84; 
VARIABLE:
  NAMES = pow1-pow4 risk1-risk3 x1_p x2_h x1x2 ;
MODEL:
! Measurement Model
  power@1;                   ! Latent variable "power" with variance fixed @1
  power BY pow1* pow2-pow4;  ! four indicators of "power" (* to estimate freely) 
  risk@1;                    ! Latent variable "risk" with variance fixed @1
  risk BY risk1* risk2-risk3;! three indicators of "risk" (* to estimate freely)
! Structural Model
  power ON x1_p (a1);        ! effect of x1_p on "power," label path (a1)
  power ON x2_h (a2);        ! effect of x1_h on "power," label path (a2)
  power ON x1x2 (a3);        ! effect of x1x2 on "power," label path (a3)
  risk ON power (b);         ! b-path "power" -> "risk", label (b)
  risk ON x1_p (cp1);        ! c'1-path
  risk ON x2_h (cp2);        ! c'2-path
  risk ON x1x2 (cp3);        ! c'3-path
! below make new variables, referring to paths (labels)
MODEL CONSTRAINT:
  new (a1b a2b a3b cprime1 cprime2 cprime3 totX1X2 total dif1 pmX1X2);
  a1b = a1*b;                    ! indirect effect of x1
  a2b = a2*b;                    ! indirect effect of x2
  a3b = a3*b;                    ! moderated mediation effect
  cprime1 = cp1;                 ! c'1
  cprime2 = cp2;                 ! c'2
  cprime3 = cp3;                 ! c'3
  totX1X2 = a3*b + cp3;          ! total effect of x1x2 on y ("risk")       
  total = a1b+a2b+a3b+cp1+cp2+cp3; ! total effect of all xs on y
  dif1 = a3b - cp3;              ! diff. indirect and conditional direct effect
  pmX1X2 = (a3b/(a3b+cp3))*100;  ! percentage mediated of total effect of X1X2 
OUTPUT:   
  STDY;                          ! R-square
  SAMPSTAT;                      ! sample statistics
