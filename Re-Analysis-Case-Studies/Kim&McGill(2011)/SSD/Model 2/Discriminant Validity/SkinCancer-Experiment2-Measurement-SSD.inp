TITLE:
  Re-analysis of Experiment 2 in:
  Kim Sara and Ann L. McGill (2011), 
  "Gaming with Mr. Slot or Gaming with the Slot Machine?
  Power, Antropomorphism, and Risk Perception,"
  Journal of Consumer Research, 38 (1), 94-107
  ! Measurement Analysis: Reliability and Discriminant Validity
  ! Note: item-level SSD 
DATA:
  FILE = SkinCancer-Experiment2-item-SSD.dat;
  TYPE = means stdeviations correlation; ! information in the SSD
  NOBSERVATIONS = 84;                    ! sample size
VARIABLE:
  NAMES = pow1-pow4 risk1-risk3 x1_p x2_h x1x2;
  USEVARIABLES = pow1-risk3;             ! selected variables
MODEL:
! Measurement Model
  power@1; ! Latent variable is "power" with variance fixed to 1 (@1)
  power BY pow1* pow2-pow4 (l1-l4);    ! loadings of indicators, with (labels)  
  pow1-pow4 (ve1-ve4);                 ! residual variances of indicators
  risk@1;                              ! variance of "risk" fixed to 1 (one)
  risk BY risk1* risk2-risk3 (l5-l7);  ! loadings of indicators on "risk"
  risk1-risk3 (ve5-ve7);               ! residual variances of indicators of "risk"
  power WITH risk (rmy);               ! correlation between "power" and "risk"
! Below new variables are made, referring to the (labels) 
MODEL CONSTRAINT:
  new (cr_ris cr_pow ave_ris ave_pow rtrue vs robs dvi_l dvi_s_pow dvi_s_ris);
  cr_ris    = ((l5+l6+l7)^2)/(((l5+l6+l7)^2)+
              (ve5+ve6+ve7));          ! composite reliability of "risk"
  cr_pow    = ((l1+l2+l3+l4)^2)/(((l1+l2+l3+l4)^2)+
              (ve1+ve2+ve3+ve4));      ! composite reliability of "power"
  ave_pow   = (l1^2+l2^2+l3^2+l4^2)/((l1^2+l2^2+l3^2+l4^2)+
              (ve1+ve2+ve3+ve4));      ! average variance extracted by "power" 
  ave_ris   = (l5^2+l6^2+l7^2)/((l5^2+l6^2+l7^2)+
              (ve5+ve6+ve7));          ! average variance extracted by "risk"
  rtrue     = rmy;                     ! "true" correlation "power" and "risk"
  vs        = rmy^2;                   ! variance shared
  robs      = rmy*sqrt(cr_pow*cr_ris); ! observed correlation
  dvi_l     = 1 - (sqrt(rtrue^2));     ! lenient discrim validity (abs val)
  dvi_s_pow = ave_pow - vs;            ! strict discrim validity of "power"
  dvi_s_ris = ave_ris - vs;            ! strict discrim validity of "risk" 
OUTPUT:   
  STDY;                                ! R-SQUARE
  SAMPSTAT;                            ! sample statistics
